allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'org.msse.demo'
    version = '0.1.0'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }

    dependencies {}

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.16.9"
    }
}

apply plugin: 'docker-compose'

dockerCompose {
    // ./gradlew postgresComposeUp|postgresComposeDown|postgresComposeDownForced
    postgres {
        useComposeFiles = ['./postgres/local/docker-compose.yml']
    }
    // ./gradlew kafkaComposeUp|kafkaComposeDown|kafkaComposeDownForced
    kafka {
        useComposeFiles = [
                './kafka/local/cluster/docker-compose-confluent.yml',
                './kafka/local/cluster/redis/docker-compose.yml'
        ]
    }
    // ./gradlew redpandaComposeUp|redpandaComposeDown|redpandaComposeDownForced
    redpanda {
        useComposeFiles = [
                './kafka/local/cluster/docker-compose-redpanda.yml',
                './kafka/local/cluster/redis/docker-compose.yml'
        ]
    }
    // ./gradlew redisComposeUp|redisComposeDown|redisComposeDownForced
    redis {
        useComposeFiles = [
                './kafka/local/cluster/redis/docker-compose.yml'
        ]
    }

    tracing {
        useComposeFiles = [
                './observability/jaeger/docker-compose.yml'
        ]
    }
}

task dockerCreateNetwork(type: Exec) {
    workingDir "$projectDir/kafka/local"
    commandLine 'sh', './network.sh'
}

task fullComposeUp {
    dependsOn 'postgresComposeUp'
    dependsOn 'kafkaComposeUp'
}

task fullComposeDown {
    dependsOn 'postgresComposeDownForced'
    dependsOn 'kafkaComposeDownForced'
}

// ensure the docker network is created
postgresComposeUp.dependsOn dockerCreateNetwork
kafkaComposeUp.dependsOn dockerCreateNetwork
redpandaComposeUp.dependsOn dockerCreateNetwork
fullComposeUp.dependsOn dockerCreateNetwork
tracingComposeUp.dependsOn dockerCreateNetwork