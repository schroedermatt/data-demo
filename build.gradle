buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.16.9"
    }
}

apply plugin: 'docker-compose'

dockerCompose {
    // ./gradlew postgresComposeUp|postgresComposeDown|postgresComposeDownForced
    postgres {
        useComposeFiles = ['./postgres/local/docker-compose.yml']
    }
    // ./gradlew kafkaComposeUp|kafkaComposeDown|kafkaComposeDownForced
    kafka {
        useComposeFiles = [
                './kafka/local/cluster/docker-compose.yml',
                './kafka/local/cluster/redis/docker-compose.yml'
        ]
    }

    tracing {
        useComposeFiles = [
                './observability/docker-compose.yml'
        ]
    }
}

task dockerCreateNetwork(type: Exec) {
    workingDir "$projectDir/kafka/local"
    commandLine 'sh', './network.sh'
}

task fullComposeUp {
    dependsOn 'postgresComposeUp'
    dependsOn 'kafkaComposeUp'
}

task fullComposeDown {
    dependsOn 'postgresComposeDownForced'
    dependsOn 'kafkaComposeDownForced'
}

// ensure the docker network is created
postgresComposeUp.dependsOn dockerCreateNetwork
kafkaComposeUp.dependsOn dockerCreateNetwork
fullComposeUp.dependsOn dockerCreateNetwork
tracingComposeUp.dependsOn dockerCreateNetwork